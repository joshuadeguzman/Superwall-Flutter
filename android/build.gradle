plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

group 'com.superwall.superwallkit_flutter'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 26
        
        // Read from local.properties
        def localPropertiesFile = rootProject.file('local.properties')
        def localProperties = new Properties()
        if (localPropertiesFile.exists()) {
            localPropertiesFile.withInputStream { localProperties.load(it) }
        }

        // Set a default value if the property is not found
        def waitForDebugger = localProperties.getProperty('WAIT_FOR_DEBUGGER', 'false')
        buildConfigField "boolean", "WAIT_FOR_DEBUGGER", waitForDebugger
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }

    buildFeatures {
        buildConfig true
    }

    // Check if the namespace property is available
    if (project.android.hasProperty("namespace")) {
        namespace 'com.superwall.superwallkit_flutter'
    }
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.0.0'

    implementation "com.superwall.sdk:superwall-android:1.3.0"
    implementation 'com.android.billingclient:billing:6.1.0'
}